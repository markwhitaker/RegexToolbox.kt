buildscript {
    ext.junit_version = '4.12'
    ext.mockk_version = '1.9.3.kotlin12'
}

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
    id 'com.palantir.git-version' version '0.11.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    jcenter()
}

group 'uk.co.mainwave.regextoolbox'
version gitVersion()

sourceCompatibility = 1.8

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = project.hasProperty('bintray_user') ? project.property('bintray_user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'Maven'
        name = project.name
        version {
            name = gitVersion()
            desc = 'Regular expression tools for Kotlin developers'
            vcsTag = gitVersion()
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
